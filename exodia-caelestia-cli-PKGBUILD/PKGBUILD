#
# Maintainer: Mahmoud Mohamed (00xWolf) <mmsaeed509@gmail.com> , <https://github.com/mmsaeed509>
#

pkgname='exodia-caelestia-cli'
pkgver=1.0.1
pkgrel=1
pkgdesc='The main cli for the Caelestia dotfiles'
arch=('any')
url='https://github.com/caelestia-dots/cli'
license=('GPL-3.0-only')
depends=(
    'python' 'python-pillow' 'gpu-screen-recorder'
    'libnotify' 'swappy' 'grim' 'dart-sass' 'dconf'
    'python-materialyoucolor' 'cliphist' 'fish'
    'app2unit' 'wl-clipboard' 'slurp' 'fuzzel' 'glib2'
)
makedepends=('python-build' 'python-installer' 'python-hatch' 'python-hatch-vcs')
provides=('caelestia-cli')
conflicts=('caelestia-cli-git')
_archive="caelestia-$pkgver"
source=("$url/releases/download/v$pkgver/$_archive.tar.gz")
sha256sums=('SKIP')

build() {
    cd "${srcdir}/${_archive}"
    /usr/bin/python -m build --wheel --no-isolation
}

package() {
    cd "${srcdir}/${_archive}"
    /usr/bin/python -m installer --destdir="$pkgdir" dist/*.whl
    install -Dm644 ./completions/caelestia.fish "$pkgdir"/usr/share/fish/vendor_completions.d/caelestia.fish
}
